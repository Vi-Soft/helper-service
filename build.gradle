plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

group = 'com.visoft'
version = '0.0.1-SNAPSHOT'

def getGitCommitId () {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '--format=%H', '-1'
        standardOutput = stdout
    }

    return stdout.toString().substring(0, 7)
}

project.archivesBaseName = project.name + '-' + getGitCommitId()



task updateVersions {
    ext.outputFile = file("src/main/resources/version.properties")
    outputs.file(outputFile)
    doLast {
        outputFile.text = "application.version=${project.version}"
    }
}
jar.dependsOn(updateVersions)

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

repositories {
    mavenCentral()
}

processResources {
    from("$rootDir/lombok.config") // https://stackoverflow.com/a/51465038
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

compileJava {
    options.compilerArgs = [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.unmappedTargetPolicy=IGNORE'
    ]
}

apply from: 'versions.gradle'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
//    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.3.6.RELEASE'
//    implementation 'org.springframework.cloud:spring-cloud-config-client:2.2.4.RELEASE'

    implementation "io.springfox:springfox-swagger2:${springfox}"
    implementation  "io.springfox:springfox-swagger-ui:${springfox}"
    implementation  "io.springfox:springfox-bean-validators:${springfox}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "io.jsonwebtoken:jjwt:${jwtVersion}"
    implementation "org.liquibase:liquibase-core"
    implementation 'ch.qos.logback:logback-core:1.2.10'
    implementation 'de.siegmar:logback-gelf:2.0.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor("org.mapstruct:mapstruct-processor:${mapstructVersion}")
}
